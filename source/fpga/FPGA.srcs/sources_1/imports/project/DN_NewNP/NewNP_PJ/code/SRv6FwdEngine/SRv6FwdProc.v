/*---------------------------------------------------------------------
    Copyright (c) 2016-2030 C2Comm, Inc.  All rights reserved.
-----------------------------------------------------------------------

Vendor C2Comm
Version 1.0
Filename  SRv6FwdProc.v
Target Device All
Dscription 
    (1)	æ¥æ”¶å¿«é?Ÿäº¤æ¢æ¨¡å—æŸ¥è¡¨å¹¶å¾…è½¬å‘çš„SRv6åˆ†ç»„è¿›è¡Œè¾“å‡ºé¢„å¤„ç?
    (2)	é’ˆå¯¹å¾…è¾“å‡ºçš„SRv6åˆ†ç»„é¢„å¤„ç†ï¼Œå®ç°è½¬å‘åŠ é?Ÿï¼ŒåŒ…æ‹¬
        1)	IPv6å¤´éƒ¨çš„ç›®çš„IPåœ°å€æ›¿æ¢
        2)	SRHçš„SLåŸŸä¿®æ”?
Data_Struct: 
    1)Metadata    
        [127:120] è¾“å…¥ç«¯å£å? 8	è¾“å…¥ç«¯å£å?
        [119:112] è¾“å‡ºç«¯å£å? 8	è¾“å‡ºç«¯å£å?
        [111:101] é•¿åº¦      11	æŠ¥æ–‡é•¿åº¦
        [100: 99] é¥æµ‹æ ‡è®°  2	é¥æµ‹æ ‡è®°ï¼Œ[99]è¾“å…¥é¥æµ‹æ ‡è®°ï¼Œ[100]è¾“å‡ºé¥æµ‹æ ‡è®°
        [     98] æ•´å½¢æ ‡è®°  1   æ•´å½¢è½¬å‘æ ‡è®°  1ä»£è¡¨æ•´å½¢
        [ 97: 88] ä¿ç•™      10	ä¿ç•™ä½?
        [ 87: 84] å¸§ç±»å?	4	æ ¹æ®åˆ†ç»„çš„C-TAGæˆ–ä»¥å¤ªç½‘ç±»å‹å­—æ®µåŒºåˆ†çš„å¸§ç±»å‹
            0:IPv6-SRv6å¸?
            1:IPv6-å…¶ä»–å¸?
            2:é”™è¯¯IPv6æŠ¥æ–‡ï¼Œä¸ŠæŠ¥CPU
            3:é”™è¯¯SRv6æŠ¥æ–‡ï¼Œä¸ŠæŠ¥CPU
            4:å‡ºå£èŠ‚ç‚¹SRv6æŠ¥æ–‡ï¼Œä¸ŠæŠ¥CPU
            5-7:ä¿ç•™
            8:TSMPå¸?
            9-14:ä¿ç•™
            15:ä¸¢å¼ƒæŠ¥æ–‡
        [ 83: 64] æµID	20	FlowID
            TSMPæŠ¥æ–‡æå–è‡ªDMACçš„MID
            IPv6æŠ¥æ–‡æå–è‡ªFlowLabel
        [ 63: 48] æµç¼–å? 16	
            SRv6æŠ¥æ–‡æ—¶æå–è‡ªSRHå¤´éƒ¨çš„Tag
            å…¶ä»–æŠ¥æ–‡æš‚æ— æ„ä¹‰
        [ 47:  0] æ—¶é—´æˆ?
    2)des_axis_tuser
        0:è½¬å‘æŠ¥æ–‡
        1:æœ¬æœºæŠ¥æ–‡
        2:é”™è¯¯IPv6æŠ¥æ–‡ï¼Œä¸ŠæŠ¥CPU
        3:é”™è¯¯SRv6æŠ¥æ–‡ï¼Œä¸ŠæŠ¥CPU
    3)des_axis_tdata
        [143:136] hdr_ext_len
        [135:128] ipv6/srv6-nh
        [127:  0] srv6-segment_list[sel]
Caution:
    1)é»˜è®¤è¾“å…¥çš„å…¨éƒ¨ä¸ºSRv6ï¼Œä¸”å·²ç»å‰¥ç¦»ä»¥å¤ªç½‘å¤´(64bitå¯¹é½)
    2)æ³¨æ„ï¼Œé»˜è®¤SRv6å¤´ä¹‹åè‡³å°‘è¦æœ?1å­—èŠ‚çš„payloadï¼Œå¦åˆ™ä¼šå‡ºç°ç²˜åŒ…é—®é¢˜(å°¾éƒ¨å¤„ç†å‡ºé”™)
Author : lxj
Revision List:
    rn2:    date:   modifier:   description:
    rn2:    date:   modifier:   description:
*/
module SRv6FwdProc(
//æ—¶é’Ÿå¤ä½ä¿¡å·
    input  wire         axis_clk,
    input  wire         axis_rst_n,
//æŠ¥æ–‡åˆ†ç»„æ•°æ®æ¥æ”¶æ¥å£
(*mark_debug="true"*)    output reg          s_axis_tready,
(*mark_debug="true"*)    input  wire         s_axis_tvalid,
    input  wire [ 15:0] s_axis_tkeep ,
(*mark_debug="true"*)    input  wire [127:0] s_axis_tdata ,
(*mark_debug="true"*)    input  wire         s_axis_tlast ,
(*mark_debug="true"*)    input  wire         s_axis_tuser ,
//åˆ†ç»„æ•°æ®è¾“å‡ºæ¥å£
(*mark_debug="true"*)    input  wire         m_axis_tready,
(*mark_debug="true"*)    output wire         m_axis_tvalid,
    output wire [ 15:0] m_axis_tkeep,
(*mark_debug="true"*)    output wire [127:0] m_axis_tdata,
(*mark_debug="true"*)    output wire         m_axis_tlast,
(*mark_debug="true"*)    output wire         m_axis_tuser,
(*mark_debug="true"*)    output wire [  1:0] m_axis_tdest
);
/*-----------------------------------------------------------------------
                    ä¸­é—´å˜é‡å£°æ˜åŒºåŸŸ
-----------------------------------------------------------------------*/
//æœ¬æ¨¡å—ä¸­æ‰?æœ‰ä¸­é—´å˜é‡?(wire/reg/parameter)åœ¨æ­¤é›†ä¸­å£°æ˜  
(*mark_debug="true"*)wire         s_axis_sop;
(*mark_debug="true"*)wire [  7:0] s_axis_line_id;
(*mark_debug="true"*)wire         s_axis_eop;

(*mark_debug="true"*)wire         acf_axis_sop;
(*mark_debug="true"*)wire [  7:0] acf_axis_line_id;
(*mark_debug="true"*)wire         acf_axis_eop;

wire         int_axis_tready;
reg          int_axis_tvalid;
reg  [ 15:0] int_axis_tkeep ;
reg  [127:0] int_axis_tdata ;
reg          int_axis_tlast ;
reg  [  0:0] int_axis_tuser ;

reg          acf_axis_tready;
wire         acf_axis_tvalid;
wire [ 15:0] acf_axis_tkeep ;
wire [127:0] acf_axis_tdata ;
wire         acf_axis_tlast ;
wire [  0:0] acf_axis_tuser ;

wire [ 31:0] acf_free_space ;
wire [ 31:0] acf_used_space ;

wire         ides_axis_tready;
reg          ides_axis_tvalid;
reg  [ 17:0] ides_axis_tkeep ;
reg  [143:0] ides_axis_tdata ;
reg          ides_axis_tlast ;
reg  [  1:0] ides_axis_tuser ;

(*mark_debug="true"*)reg          odes_axis_tready;
(*mark_debug="true"*)wire         odes_axis_tvalid;
wire [ 17:0] odes_axis_tkeep ;
(*mark_debug="true"*)wire [143:0] odes_axis_tdata ;
(*mark_debug="true"*)wire         odes_axis_tlast ;
(*mark_debug="true"*)wire [  1:0] odes_axis_tuser ;

wire [ 31:0] des_free_space ;
(*mark_debug="true"*)wire [ 31:0] des_used_space ;


wire         disp_axis_tready;
reg          disp_axis_tvalid;
reg  [ 15:0] disp_axis_tkeep ;
reg  [127:0] disp_axis_tdata ;
reg          disp_axis_tlast ;
reg  [  0:0] disp_axis_tuser ;
reg  [  1:0] disp_axis_tdest ;

(*mark_debug="true"*)reg [  1:0] cur_proc_type  ,nxt_proc_type;
(*mark_debug="true"*)reg [  7:0] cur_srh_len    ,nxt_srh_len;
(*mark_debug="true"*)reg [  7:0] cur_srh_segleft,nxt_srh_segleft;
(*mark_debug="true"*)reg [  7:0] cur_srh_nh     ,nxt_srh_nh;
(*mark_debug="true"*)reg [127:0] cur_srh_seglist,nxt_srh_seglist;

(*mark_debug="true"*)reg [  7:0] last_entry_maxid;
/*-----------------------------------------------------------------------
                    å¤„ç†å­—æ®µé‡‡é›†
-----------------------------------------------------------------------*/
always @(posedge axis_clk or negedge axis_rst_n) begin
    if(axis_rst_n == 1'b0) begin
        cur_proc_type   <= 2'b0;
        cur_srh_segleft <= 8'b0;
        cur_srh_nh      <= 8'b0;
        cur_srh_seglist <= 127'b0;
    end
    else begin
        cur_proc_type   <= nxt_proc_type;
        cur_srh_len     <= nxt_srh_len  ;
        cur_srh_segleft <= nxt_srh_segleft;
        cur_srh_nh      <= nxt_srh_nh;
        cur_srh_seglist <= nxt_srh_seglist;
    end
end

always @(*) begin
    s_axis_tready     = int_axis_tready & ides_axis_tready;
    int_axis_tvalid   = s_axis_tvalid & s_axis_tready;
    int_axis_tkeep    = s_axis_tkeep ;
    int_axis_tdata    = s_axis_tdata ;
    int_axis_tlast    = s_axis_tlast ;
    int_axis_tuser    = s_axis_tuser ;

    last_entry_maxid  = (s_axis_tdata[55:49] > 7'b0) ? s_axis_tdata[55:49]-8'b1 : 8'b0;
    nxt_proc_type     = cur_proc_type  ;
    nxt_srh_len       = cur_srh_len    ;
    nxt_srh_segleft   = cur_srh_segleft;
    nxt_srh_nh        = cur_srh_nh     ;
    nxt_srh_seglist   = cur_srh_seglist;

    if((s_axis_tready == 1'b1) && (s_axis_tvalid == 1'b1)) begin 
        if(s_axis_line_id == 8'd1) begin//IPv6 Header
            nxt_srh_nh        = s_axis_tdata[79:72]  ;//å…ˆæå–IPv6çš„Next Header
            if(s_axis_tdata[71:64] > 8'd0) begin//ipv6_hop_limit
                nxt_proc_type         = 2'd0;
                int_axis_tdata[71:64] = s_axis_tdata[71:64]-1'b1;
            end
            else begin//é”™è¯¯IPv6å¤?
                nxt_proc_type         = 2'd2;
            end
        end
        else if(s_axis_line_id == 8'd3) begin//SRv6 Header
            nxt_srh_segleft = s_axis_tdata[39:32];
            nxt_srh_len     = s_axis_tdata[55:48];
            if((s_axis_tdata[31:24] > last_entry_maxid) || 
               (s_axis_tdata[39:32] > (s_axis_tdata[31:24] + 1'b1))) begin
            //last_entry > last_entry_maxid or segment_left>last_entry+1ï¼Œé”™è¯¯SRv6åè®®å¤?
                nxt_proc_type = 2'd3; 
            end
            else if(s_axis_tdata[39:32] > 8'd0) begin//segment_left
                nxt_proc_type = 2'd0; 
                int_axis_tdata[39:32] = s_axis_tdata[39:32]-1'b1;
            end
            else begin//æœ¬æœº
                nxt_proc_type = 2'd1; 
                nxt_srh_nh    = s_axis_tdata[63:56]  ;//æœ¬æœºè½¬å‘æŠ¥æ–‡éœ?è¦æå–å¹¶æ›¿æ¢SRv6çš„NH
            end
        end
        else if(s_axis_line_id == (cur_srh_segleft + 4'd3)) begin//æŠ¥æ–‡ä¸­çš„srh_segleft+å¤´éƒ¨4æ‹?,ä¸èƒ½å‡?1,åŸå› è¯¦è§<NewNPè®¾è®¡>-2.2.1 SRv6å¤´çš„åŸç†
            nxt_srh_seglist = s_axis_tdata;
        end
    end
end

//è¾“å…¥æŠ¥æ–‡æè¿°ç¬¦å¤„ç?
always @(*) begin
    ides_axis_tvalid = int_axis_tlast & int_axis_tvalid ;//        
    ides_axis_tkeep  = {17{1'b1}};//[ 16:0] 
    ides_axis_tdata  = {nxt_srh_len,nxt_srh_nh,nxt_srh_seglist};//[135:0] 
    ides_axis_tlast  = 1'b1;//        
    ides_axis_tuser  = nxt_proc_type;//[  1:0] 
end

/*-----------------------------------------------------------------------
                    è¾“å‡ºæŠ¥æ–‡å¤„ç†
-----------------------------------------------------------------------*/
(*mark_debug="true"*)reg [3:0] cur_trans_state,nxt_trans_state;
reg [1:0] disp_axis_tdest_reg;

localparam  START_S   = 4'd0,
            REPLACE_S = 4'd1,
            DEL_SEG_S = 4'd2,
            TRANS_S   = 4'd3;
always @(posedge axis_clk or negedge axis_rst_n) begin
    if(axis_rst_n == 1'b0) begin
        disp_axis_tdest_reg   <= 2'b0;
        cur_trans_state       <= START_S;
    end
    else begin
        disp_axis_tdest_reg   <= disp_axis_tdest;
        cur_trans_state       <= nxt_trans_state;
    end
end

always @(*) begin
    disp_axis_tkeep = acf_axis_tkeep;
    disp_axis_tdata = acf_axis_tdata;
    disp_axis_tlast = acf_axis_tlast;
    disp_axis_tuser = acf_axis_tuser;
    disp_axis_tdest = disp_axis_tdest_reg;
    case(cur_trans_state)
        START_S: begin
            if((acf_axis_tvalid == 1'b1) && 
               (odes_axis_tvalid == 1'b1) && 
               (disp_axis_tready == 1'b1)) begin //æœ‰æŠ¥æ–‡å®Œæˆé¢„å¤„ç†ï¼Œä¸”å¯¹ç«¯å¯æ¥æ”?
                acf_axis_tready  = 1'b1;
                disp_axis_tvalid = 1'b1;
                disp_axis_tdest = (odes_axis_tuser == 2'd1) ? 2'b01:2'b10;//ä»…æœ¬æœºæŠ¥æ–‡éœ€è¦åˆ é™¤SRHå¤?
                
                disp_axis_tdata[98] = odes_axis_tdata[63]; //ä¿®æ”¹MDä¸­çš„æ•´å½¢æ ‡è®°ï¼?20230111
                
                if(odes_axis_tuser < 2'd2) begin//è½¬å‘æŠ¥æ–‡æˆ–æœ¬æœºæŠ¥æ–?
                //æ›¿æ¢IPv6çš„NHå’ŒDIP
                    odes_axis_tready = 1'b0;
                    //æœ¬æœºæŠ¥æ–‡ç»è¿‡è¯¥æ¨¡å—åˆ é™¤SRHå¤´åï¼Œéœ€è¦å°†Metadata.Typeéœ?è¦ä¿®æ”¹ä¸ºIPv6-å…¶ä»–å¸§ï¼Œä»¥ä¾¿å†æ¬¡æŸ¥è¡¨è½¬å‘
                    disp_axis_tdata[87:84] = (odes_axis_tuser == 2'd1) ? 4'd1:disp_axis_tdata[87:84];
                    nxt_trans_state  = REPLACE_S;
                end
                else begin//é”™è¯¯IPv6æŠ¥æ–‡æˆ–é”™è¯¯SRv6æŠ¥æ–‡ï¼Œä¸ŠæŠ¥CPU
                    odes_axis_tready = 1'b1;
                    disp_axis_tdata[87:84] = {2'b0,odes_axis_tuser};
                    nxt_trans_state = TRANS_S;
                end
            end
            else begin//æŠ¥æ–‡æœªå®Œæˆå¤„ç?
                acf_axis_tready  = 1'b0;
                odes_axis_tready = 1'b0;
                disp_axis_tvalid    = 1'b0;
                nxt_trans_state  = START_S;
            end
        end
        
        REPLACE_S: begin
            acf_axis_tready  = disp_axis_tready;
            disp_axis_tvalid = acf_axis_tvalid;
            if((acf_axis_tvalid == 1'b1) && (acf_axis_tready == 1'b1)) begin
                if(acf_axis_line_id == 8'd1) begin//IPv6-Header,æ›¿æ¢NH
                    odes_axis_tready = 1'b0;
                    disp_axis_tdata[95:80] = (disp_axis_tdest_reg == 2'b01) ? 
                                              (disp_axis_tdata[95:80] - {odes_axis_tdata[143:136]+1'b1,3'b0}) : disp_axis_tdata[95:80];//ä¿®æ”¹IPv6 Payload_Len(åŸæœ¬çš„é•¿åº¦å‡å»SRHé•¿åº¦)
                    disp_axis_tdata[79:72] = odes_axis_tdata[135:128];//æ›¿æ¢NH
                    nxt_trans_state  = REPLACE_S;
                end
                else if(acf_axis_line_id == 8'd2) begin//æ›¿æ¢DIP[127:64]
                    odes_axis_tready = 1'b0;
                    disp_axis_tdata[63:0] = odes_axis_tdata[127:64];
                    nxt_trans_state  = REPLACE_S;
                end
                else begin// (acf_axis_line_id == 8'd3) IPv6-DIP[63:0]
                    disp_axis_tdata[127:64] = odes_axis_tdata[63:0];//æ›¿æ¢DIP[63:0]
                    if(disp_axis_tdest_reg == 2'b01) begin//å‰”é™¤SRHå¤´ä¸­Segment_listçš?(æœ?64bitæ§åˆ¶å­—æ®µæœªå‰”é™¤ï¼Œåç»­é‡‡ç”¨AXISRemoveç»§ç»­)
                        odes_axis_tready = 1'b0;//odeså®Œæˆä»»åŠ¡å¯ä»¥è¢«è¯»å‡?
                        nxt_trans_state  = DEL_SEG_S;
                    end
                    else begin
                        odes_axis_tready = 1'b1;//odeså®Œæˆä»»åŠ¡å¯ä»¥è¢«è¯»å‡?
                        nxt_trans_state  = TRANS_S;
                    end
                end
            end
            else begin
                odes_axis_tready = 1'b0;
                nxt_trans_state  = REPLACE_S;
            end
        end
        
        DEL_SEG_S: begin
            acf_axis_tready  = 1'b1;
            if((acf_axis_tvalid == 1'b1) && 
               ((acf_axis_line_id == (odes_axis_tdata[143:137]+4'd3)) || (acf_axis_tlast == 1'b1))) begin
                disp_axis_tvalid = 1'b0;
                odes_axis_tready = 1'b1;
                nxt_trans_state  = TRANS_S;//é»˜è®¤SRv6å¤´ä¹‹åè‡³å°‘è¦æœ?1å­—èŠ‚çš„payloadï¼Œå¦åˆ™ä¼šå‡ºç°ç²˜åŒ…é—®é¢˜(å°¾éƒ¨å¤„ç†å‡ºé”™)
            end
            else begin
                disp_axis_tvalid = 1'b0;
                odes_axis_tready = 1'b0;
                nxt_trans_state  = DEL_SEG_S;
            end
        end
        
        TRANS_S: begin
            acf_axis_tready  = disp_axis_tready;
            disp_axis_tvalid = acf_axis_tvalid;
            odes_axis_tready = 1'b0;
            if((acf_axis_tvalid == 1'b1) && (acf_axis_tready == 1'b1) && (acf_axis_tlast == 1'b1)) begin
                nxt_trans_state  = START_S;
            end
            else begin
                nxt_trans_state  = TRANS_S;
            end
        end
        
        default: begin
            acf_axis_tready  = 1'b0;
            disp_axis_tvalid = 1'b0;
            odes_axis_tready = 1'b0;
            nxt_trans_state  = START_S;
        end
    endcase
end
/*-----------------------------------------------------------------------
                    IPè°ƒç”¨åŒºåŸŸ
-----------------------------------------------------------------------*/
//æœ¬æ¨¡å—è°ƒç”¨çš„æ‰?æœ‰IPåœ¨è¯¥åŒºåŸŸå®ä¾‹åŒ?
//ä¾‹å¦‚fifo/ram/grantä¹‹ç±»çš„IP.... 
PktLineDet #(8) SaxisLineDet_Inst(
    .user_clk               ( axis_clk          ),
    .user_rst_n             ( axis_rst_n        ),
                            
    .valid                  ( s_axis_tready & s_axis_tvalid),
    .last                   ( s_axis_tlast      ),
                            
    .sop                    ( s_axis_sop        ),
    .line_id                ( s_axis_line_id    ),
    .eop                    ( s_axis_eop        )
);

PktLineDet #(8) AcfaxisLineDet_Inst(
    .user_clk               ( axis_clk          ),
    .user_rst_n             ( axis_rst_n        ),
                            
    .valid                  ( acf_axis_tready & acf_axis_tvalid),
    .last                   ( acf_axis_tlast      ),
                            
    .sop                    ( acf_axis_sop        ),
    .line_id                ( acf_axis_line_id    ),
    .eop                    ( acf_axis_eop        )
);

AXISFifoBuf #(
    .ASYNC_EN               ( 0               ),
    .DEEP_WIDTH             ( 9               ),
    .PKT_MODE               ( 1               ),
    .DATA_BYTE              ( 16              ),
    .USER_BIT               ( 1               ),
    .MIN_PKTLINE            ( 4               )
)
RxFifoBuf(  
    //ports 
    .s_axis_clk             ( axis_clk        ),
    .s_axis_rst_n           ( axis_rst_n      ),
        
    .m_axis_clk             ( axis_clk        ),
    .m_axis_rst_n           ( axis_rst_n      ),
        
    .s_axis_tready          ( int_axis_tready ),
    .s_axis_tvalid          ( int_axis_tvalid ),
    .s_axis_tkeep           ( int_axis_tkeep  ),
    .s_axis_tdata           ( int_axis_tdata  ),
    .s_axis_tlast           ( int_axis_tlast  ),
    .s_axis_tuser           ( int_axis_tuser  ),
        
    .m_axis_tready          ( acf_axis_tready ),
    .m_axis_tvalid          ( acf_axis_tvalid ),
    .m_axis_tkeep           ( acf_axis_tkeep  ),
    .m_axis_tdata           ( acf_axis_tdata  ),
    .m_axis_tlast           ( acf_axis_tlast  ),
    .m_axis_tuser           ( acf_axis_tuser  ),
                                              
    .free_space             ( acf_free_space  ),
    .used_space             ( acf_used_space  )
); 

AXISFifoBuf #(
    .ASYNC_EN               ( 0               ),
    .DEEP_WIDTH             ( 7               ),
    .PKT_MODE               ( 1               ),
    .DATA_BYTE              ( 18              ),
    .USER_BIT               ( 2               ),
    .MIN_PKTLINE            ( 1               )
)
DesFifoBuf(  
    //ports 
    .s_axis_clk             ( axis_clk        ),
    .s_axis_rst_n           ( axis_rst_n      ),
        
    .m_axis_clk             ( axis_clk        ),
    .m_axis_rst_n           ( axis_rst_n      ),
        
    .s_axis_tready          ( ides_axis_tready   ),
    .s_axis_tvalid          ( ides_axis_tvalid   ),
    .s_axis_tkeep           ( ides_axis_tkeep    ),
    .s_axis_tdata           ( ides_axis_tdata    ),
    .s_axis_tlast           ( ides_axis_tlast    ),
    .s_axis_tuser           ( ides_axis_tuser    ),
        
    .m_axis_tready          ( odes_axis_tready ),
    .m_axis_tvalid          ( odes_axis_tvalid ),
    .m_axis_tkeep           ( odes_axis_tkeep  ),
    .m_axis_tdata           ( odes_axis_tdata  ),
    .m_axis_tlast           ( odes_axis_tlast  ),
    .m_axis_tuser           ( odes_axis_tuser  ),
                                              
    .free_space             ( des_free_space  ),
    .used_space             ( des_used_space  )
);

AXISRegBuf#(
    .PROMODE_EN(1),
    .DATA_BYTE (16),
    .USER_BIT  (3)
)AXISRegBuf_Inst(
//æ—¶é’Ÿå¤ä½ä¿¡å·
    .axis_clk  (axis_clk  ),
    .axis_rst_n(axis_rst_n),
//AXI Streamè¾“å…¥æ¥å£
    .s_axis_tready(disp_axis_tready),
    .s_axis_tvalid(disp_axis_tvalid),
    .s_axis_tkeep (disp_axis_tkeep ),
    .s_axis_tdata (disp_axis_tdata ),
    .s_axis_tlast (disp_axis_tlast ),
    .s_axis_tuser ({disp_axis_tuser,disp_axis_tdest}),    
//AXI Streamè¾“å‡ºæ¥å£
    .m_axis_tready(m_axis_tready),
    .m_axis_tvalid(m_axis_tvalid),
    .m_axis_tkeep (m_axis_tkeep ),
    .m_axis_tdata (m_axis_tdata ),
    .m_axis_tlast (m_axis_tlast ),
    .m_axis_tuser ({m_axis_tuser,m_axis_tdest})
);
endmodule
/*
SRv6FwdProc SRv6FwdProc_Inst(  
    .axis_clk             (),
    .axis_rst_n           (),
    
    .s_axis_tready        (),
    .s_axis_tvalid        (),
    .s_axis_tkeep         (),
    .s_axis_tdata         (),
    .s_axis_tlast         (),
    .s_axis_tuser         (),
                          
    .m_axis_tready        (),
    .m_axis_tvalid        (),
    .m_axis_tkeep         (),
    .m_axis_tdata         (),
    .m_axis_tlast         (),
    .m_axis_tuser         ()
);
*/